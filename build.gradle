plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.whispr'
version = '0.1'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
//DEPENDENCIES
dependencies {
//    implementation 'io.github.palexdev:materialfx:11.17.0'

    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'ch.qos.logback:logback-classic:1.5.13'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

//JAVA FX
javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

//MAIN APP CONFIG
application {
    mainClass = 'com.whispr.securechat.client.gui.MainApplication'
}

test {
    useJUnitPlatform()
}

//CUSTOM RUN TASKS
tasks.register('runServer', JavaExec) {
    group = 'Application'
    description = 'Runs the chat server (non-GUI)'
    mainClass = 'com.whispr.securechat.server.ChatServer'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runAdmin', JavaExec) {
    group = 'Application'
    description = 'Runs the Admin GUI application'
    mainClass = 'com.whispr.securechat.admin.gui.AdminApplication'
    classpath = sourceSets.main.runtimeClasspath + configurations.runtimeClasspath

    // Jawne dodanie argumentów JVM dla JavaFX
    jvmArgs = [
            '--module-path', classpath.filter { it.name.contains('javafx') }.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml'
    ]
}

tasks.register('runClientTerminal', JavaExec) {
    group = 'Application'
    description = 'Runs the interactive terminal client'
    mainClass = 'com.whispr.securechat.client.ChatClient'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

//CONFIG TASKS
tasks.register('setupAdmin', JavaExec) {
    group = 'Utilities' // Osobna grupa dla narzędzi
    description = 'Runs the one-time admin account setup'
    mainClass = 'com.whispr.securechat.setup.OneTimeAdminSetup'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runRSAEncryptionUtil', JavaExec) {
    group = 'Utilities'
    description = 'Runs the RSA encryption test'
    mainClass = 'com.whispr.securechat.security.RSAEncryptionUtil'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runAESEncryptionUtil', JavaExec) {
    group = 'Utilities'
    description = 'Runs the AES encryption test'
    mainClass = 'com.whispr.securechat.security.AESEncryptionUtil'
    classpath = sourceSets.main.runtimeClasspath
}