plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.whispr'
version = '0.1'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
//DEPENDENCIES
dependencies {
//    implementation 'io.github.palexdev:materialfx:11.17.0'

    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'ch.qos.logback:logback-classic:1.5.13'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
    testImplementation("org.mockito:mockito-core:5.11.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.11.0")
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

//JAVA FX
javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

//MAIN APP CONFIG
application {
    mainClass = 'com.whispr.securechat.client.gui.MainApplication'
}

test {
    // Użyj platformy JUnit 5 do uruchamiania testów.
    useJUnitPlatform(){
        version = '5.10.3'
    }

    // Skonfiguruj logowanie wyników testów w konsoli.
    testLogging {
        // Pokazuj status dla testów, które przeszły, zostały pominięte lub zakończyły się błędem.
        events "passed", "skipped", "failed"
    }
}

//CUSTOM RUN TASKS
tasks.register('runServer', JavaExec) {
    group = 'Application'
    description = 'Runs the chat server (non-GUI)'
    mainClass = 'com.whispr.securechat.server.ChatServer'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runAdmin', JavaExec) {
    group = 'Application'
    description = 'Runs the Admin GUI application'
    mainClass = 'com.whispr.securechat.admin.gui.AdminApplication'
    classpath = sourceSets.main.runtimeClasspath + configurations.runtimeClasspath

    // Jawne dodanie argumentów JVM dla JavaFX
    jvmArgs = [
            '--module-path', classpath.filter { it.name.contains('javafx') }.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml'
    ]
}

tasks.register('runClientTerminal', JavaExec) {
    group = 'Application'
    description = 'Runs the interactive terminal client'
    mainClass = 'com.whispr.securechat.client.ChatClient'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

//CONFIG TASKS
tasks.register('setupAdmin', JavaExec) {
    group = 'Utilities' // Osobna grupa dla narzędzi
    description = 'Runs the one-time admin account setup'
    mainClass = 'com.whispr.securechat.setup.OneTimeAdminSetup'
    classpath = sourceSets.main.runtimeClasspath
}